buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        maven {
            url "https://repo.spongepowered.org/repository/maven-public/"
            content { includeGroup "org.spongepowered" }
        }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
    }
}

plugins {
    id 'java'
    id 'idea'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'

def envVersion = System.getenv('VERSION')
version = envVersion == null ? '0.0.0-indev' : envVersion
archivesBaseName = "${mod_id}-forge-${minecraft_version}"

minecraft {
    mappings channel: 'official', version: minecraft_version

    runs {
        client {
            workingDirectory project.file('run')
            ideaModule "${rootProject.name}.${project.name}.main"
            taskName 'Client'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
            mods {
                modClientRun {
                    source sourceSets.main
                    source project(":Common").sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            ideaModule "${rootProject.name}.${project.name}.main"
            taskName 'Server'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
            mods {
                modServerRun {
                    source sourceSets.main
                    source project(":Common").sourceSets.main
                }
            }
        }
    }
}

configurations {
    shadowLibrary
    minecraftLibrary.extendsFrom(shadowLibrary)
}

repositories {
    mavenLocal()
    maven {
        url = 'https://alcatrazescapee.jfrog.io/artifactory/mods'
    }
    flatDir {
        dirs 'libs'
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

    if (System.getProperty("idea.sync.active") != "true") {
        annotationProcessor "org.spongepowered:mixin:0.8.5:processor"
    }

    compileOnly project(":Common")

    // runtimeOnly fg.deobf("libs:DynamicTrees-1.18.2:0.11.0-Alpha2")
    // runtimeOnly fg.deobf("libs:ProjectE-1.18.2:PE1.0.1")

    shadowLibrary "com.alcatrazescapee:epsilon:${epsilon_version}"
}

mixin {
    add sourceSets.main, "${mod_id}.refmap.json"

    config "${mod_id}.mixins.json"
    config "${mod_id}.common.mixins.json"
}

tasks.withType(JavaCompile) {
    source(project(":Common").sourceSets.main.allSource)
}

processResources {
    from project(":Common").sourceSets.main.resources
}

jar.finalizedBy('reobfJar')

jar {
    classifier 'slim'
}

shadowJar {
    classifier ''
    configurations = [project.configurations.shadowLibrary]
    dependencies {
        exclude(dependency { it.moduleGroup != 'com.alcatrazescapee' })
    }
    relocate 'com.alcatrazescapee.epsilon', 'com.alcatrazescapee.notreepunching.epsilon'
    finalizedBy 'reobfShadowJar'
}

assemble.dependsOn(shadowJar)

reobf {
    shadowJar {}
}
