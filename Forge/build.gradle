plugins {
    id "java"
    id "idea"
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id "net.minecraftforge.gradle" version "5.1.+"
    id "org.spongepowered.mixin" version "0.7-SNAPSHOT"
    id "org.parchmentmc.librarian.forgegradle" version "1.+"
}

def envVersion = System.getenv("VERSION")
version = envVersion == null ? "0.0.0-indev" : envVersion
archivesBaseName = "${mod_id}-forge-${minecraft_version}"

minecraft {
    mappings channel: "parchment", version: "${parchment_version}-${minecraft_version}"

    runs {
        client {
            workingDirectory project.file("run")
            ideaModule("${rootProject.name}.${project.name}.main")
            taskName("Client")
            property("mixin.env.remapRefMap", "true")
            property("mixin.env.refMapRemappingFile", "${projectDir}/build/createSrgToMcp/output.srg")
            mods {
                modClientRun {
                    source sourceSets.main
                    source project(":Common").sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            ideaModule("${rootProject.name}.${project.name}.main")
            taskName("Server")
            property("mixin.env.remapRefMap", "true")
            property("mixin.env.refMapRemappingFile", "${projectDir}/build/createSrgToMcp/output.srg")
            mods {
                modServerRun {
                    source sourceSets.main
                    source project(":Common").sourceSets.main
                }
            }
        }
    }
}

configurations {
    shadowLibrary
    minecraftLibrary.extendsFrom(shadowLibrary)
}

repositories {
    mavenLocal()
    maven {
        url = "https://alcatrazescapee.jfrog.io/artifactory/mods"
    }
    flatDir {
        dirs "libs"
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

    if (System.getProperty("idea.sync.active") != "true") {
        annotationProcessor "org.spongepowered:mixin:0.8.5:processor"
    }

    compileOnly(project(":Common"))

    shadowLibrary("com.alcatrazescapee:epsilon:${epsilon_version}")
}

mixin {
    add sourceSets.main, "${mod_id}.refmap.json"

    config "${mod_id}.mixins.json"
    config "${mod_id}.common.mixins.json"
}

// Workaround for a bug in Forge / Mixin gradle where the refmap won't be added to the jar unless Forge java compile is done
// From https://github.com/gamma-delta/HexMod/blob/main/Forge/build.gradle#L161
task invalidateJavaForRefmap {
    doFirst {
        compileJava {
            if (!didWork) {
                outputs.upToDateWhen { false }
            }
        }
    }
}

tasks.withType(JavaCompile) {
    source(project(":Common").sourceSets.main.allSource)
    shouldRunAfter(invalidateJavaForRefmap)
}

processResources {
    from project(":Common").sourceSets.main.resources
}

jar.finalizedBy('reobfJar')

jar {
    classifier "slim"
}

shadowJar {
    classifier ""
    configurations = [project.configurations.shadowLibrary]
    dependencies {
        exclude(dependency { it.moduleGroup != "com.alcatrazescapee" })
    }
    relocate "com.alcatrazescapee.epsilon", "com.alcatrazescapee.notreepunching.epsilon"
    finalizedBy "reobfShadowJar"
}

assemble.dependsOn(shadowJar)

reobf {
    shadowJar {}
}
